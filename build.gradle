import org.gradle.api.tasks.testing.logging.TestLogEvent
import io.vproxy.base.util.OS

buildscript {
    dependencies {
        classpath group: 'io.vproxy', name: 'commons', version: '1.1.3'
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'me.champeau.jmh' version "0.7.1" apply false
}

group = 'org.example'
version = loadVersion()

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation project(":pni")
}

java {
    sourceCompatibility = '11'
    targetCompatibility = '11'
}

jar {
    archiveFileName = 'pni.jar'
    manifest {
        attributes 'Main-Class': 'io.vproxy.pni.exec.Main'
    }
}

shadowJar {
    archiveBaseName = 'pni'
    archiveClassifier = ''
    archiveVersion = ''
}

task nativeImage(type: Exec) {
    if (OS.isWindows()) {
        executable 'native-image.cmd'
    } else {
        executable 'native-image'
    }
    def output = 'pni'
    if (!OS.isWindows()) {
        output += '.run'
    }
    args '-jar', './build/libs/pni.jar', '--no-fallback', '-o', output

    dependsOn shadowJar
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    group 'io.vproxy'
    version rootProject.version

    if (project.name == 'pni') {
        java {
            sourceCompatibility = '11'
            targetCompatibility = '11'
        }
    } else {
        java {
            sourceCompatibility = '21'
            targetCompatibility = '21'
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.incremental = false
        if (project.name != 'pni') {
            options.compilerArgs += '--enable-preview'
        }
    }
    tasks.withType(JavaExec) {
        if (project.name != 'pni') {
            jvmArgs += '--enable-preview'
            jvmArgs += '--enable-native-access=ALL-UNNAMED'
        }
    }
    tasks.withType(Test) {
        if (project.name != 'pni') {
            jvmArgs += '--enable-preview'
            jvmArgs += '--enable-native-access=ALL-UNNAMED'
        }

        testLogging {
            events TestLogEvent.FAILED, TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.STARTED
            maxGranularity 100
            exceptionFormat "full"
            showCauses true
            showExceptions true
            showStackTraces true
            showStandardStreams true
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    task javadocJar(type: Jar) {
        archiveClassifier = 'javadoc'
        from "$buildDir/docs/javadoc"
    }

    tasks.withType(GenerateModuleMetadata) {
        enabled = false
    }

    java {
        withSourcesJar()
    }

    if (['api', 'graal', 'pni'].contains(project.name)) {
        def releaseName = ""
        def pomName = ""
        if (project.name == 'api') {
            releaseName = 'pni-api-jdk21'
            pomName = "pni"
        } else if (project.name == 'graal') {
            releaseName = 'pni-api-graal'
            pomName = 'pni.graal'
        } else {
            releaseName = 'pni-exec'
            pomName = "pni.exec"
        }

        publishing {
            publications {
                maven(MavenPublication) {
                    from components.java
                    artifactId releaseName

                    artifact(javadocJar)

                    pom {
                        name = 'io.vproxy.' + pomName
                        description = 'panama native interface ' + releaseName
                        url = 'https://vproxy.io'
                        licenses {
                            license {
                                name = 'MIT License'
                                url = 'https://github.com/vproxy-tools/panama-native-interface/blob/master/LICENSE'
                            }
                        }
                        developers {
                            developer {
                                id = 'wkgcass'
                                name = 'K.G. Wang'
                                email = 'wkgcass@hotmail.com'
                            }
                        }
                        scm {
                            connection = 'scm:git:git://github.com/vproxy-tools/panama-native-interface.git'
                            developerConnection = 'scm:git:git://github.com/vproxy-tools/panama-native-interface.git'
                            url = 'https://github.com/vproxy-tools/panama-native-interface'
                        }
                    }
                }
            }
            repositories {
                maven {
                    credentials {
                        username 'wkgcass'
                        password System.getProperty("MavenPublishPassword")
                    }
                    def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                    def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                    url = version.contains('-DEV') ? snapshotsRepoUrl : releasesRepoUrl
                }
            }
        }
        if (!System.getProperty("MavenLocalInstall", "false").equals("true")) {
            signing {
                sign publishing.publications.maven
            }
        }
    }
}

project(":api") {
    task compileLib(type: Exec) {
        workingDir project.rootProject.rootDir.getAbsolutePath() + '/api/src/main/c'
        def bashPath = System.getenv("MINGW_BASH")
        if (bashPath == null)
            bashPath = '/bin/bash'
        executable bashPath
        args './make.sh'
    }
}

project(":graal") {
    dependencies {
        api project(':api')
        compileOnly group: 'org.graalvm.sdk', name: 'nativeimage', version: '23.1.0'
    }
}

project(":pni") {
    dependencies {
        implementation 'io.vproxy:commons:1.1.3'
        implementation 'org.ow2.asm:asm:9.5'
        implementation 'org.ow2.asm:asm-tree:9.5'
    }
}

project(":unittest") {
    dependencies {
        implementation project(":api")
        implementation project(":pni")
        testImplementation 'io.vproxy:commons:1.1.3'
        testImplementation 'junit:junit:4.13.2'
    }
    task runUnitTest(type: Test) {
        jvmArgs += '-Djava.library.path=' +
                project.rootProject.rootDir.getAbsolutePath() + '/api/src/main/c'
        include '**/Suite.class'
    }
}

project(":acceptance-testing-template") {
    compileJava {
        options.compilerArgs += '-parameters'
    }
    dependencies {
        implementation project(":api")
    }

    task cleanAcceptanceTesting(type: Exec) {
        workingDir project.rootProject.rootDir.getAbsolutePath() + '/acceptance-testing/src/test/'
        commandLine 'bash', '-c', 'rm -f c/libpnitest.dylib && rm -f c/libpnitest.so && rm -f c/pnitest.dll && rm -rf c-generated/* && rm -rf generated/*'
    }
    task generateAcceptanceTesting(type: JavaExec) {
        classpath = project(":pni").sourceSets.main.runtimeClasspath
        mainClass = 'io.vproxy.pni.exec.Main'
        systemProperty("io.vproxy.pni.Testing", "true")
        workingDir project.rootProject.rootDir.getAbsolutePath()
        args '-cp', 'acceptance-testing-template/build/classes/java/main', '-d', 'acceptance-testing/src/test/generated', '-h', 'acceptance-testing/src/test/c-generated'

        dependsOn cleanAcceptanceTesting
        dependsOn compileJava
    }
}

project(":acceptance-testing") {
    sourceSets {
        test {
            java {
                srcDirs = ['src/test/java', 'src/test/generated']
            }
        }
    }
    dependencies {
        implementation project(":api")
        testImplementation 'io.vproxy:commons:1.1.3'
        testImplementation 'junit:junit:4.13.2'
    }

    task compilePNITestLib(type: Exec) {
        workingDir project.rootProject.rootDir.getAbsolutePath() + '/acceptance-testing/src/test/c'
        def bashPath = System.getenv("MINGW_BASH")
        if (bashPath == null)
            bashPath = '/bin/bash'
        executable bashPath
        args './make-test.sh'

        dependsOn project(':acceptance-testing-template').tasks.generateAcceptanceTesting
    }
    task runAcceptanceTest(type: Test) {
        jvmArgs += '-Djava.library.path=' +
                project.rootProject.rootDir.getAbsolutePath() + '/acceptance-testing/src/test/c' +
                File.pathSeparator +
                project.rootProject.rootDir.getAbsolutePath() + '/api/src/main/c'
        jvmArgs += '--add-opens'
        jvmArgs += 'java.base/java.io=ALL-UNNAMED'
        include '**/Suite.class'

        dependsOn compileTestJava
        dependsOn compilePNITestLib
    }
}

project(":graal-test-template") {
    compileJava {
        options.compilerArgs += '-parameters'
    }
    dependencies {
        implementation project(":api")
    }

    task cleanGraalTesting(type: Exec) {
        workingDir project.rootProject.rootDir.getAbsolutePath() + '/graal-test/src/main/'
        commandLine 'bash', '-c', 'rm -f c/libgraaltest.dylib && rm -f c/libgraaltest.so && rm -f c/graaltest.dll && rm -rf c-generated/* && rm -rf generated/*'
    }
    task generateGraalTesting(type: JavaExec) {
        classpath = project(":pni").sourceSets.main.runtimeClasspath
        mainClass = 'io.vproxy.pni.exec.Main'
        systemProperty("io.vproxy.pni.Testing", "true")
        workingDir project.rootProject.rootDir.getAbsolutePath()
        args '-cp', 'graal-test-template/build/classes/java/main', '-d', 'graal-test/src/main/generated', '-h', 'graal-test/src/main/c-generated',
                '-fgraal-native-image-feature=io.vproxy.pni.graal.test.Feature',
                '-fgraal-c-entrypoint-literal-upcall'

        dependsOn cleanGraalTesting
        dependsOn compileJava
    }
}

project(":graal-test") {
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java', 'src/main/generated']
            }
        }
    }
    dependencies {
        implementation project(":api")
        implementation project(":graal")
        implementation 'junit:junit:4.13.2'
        compileOnly group: 'org.graalvm.sdk', name: 'nativeimage', version: '23.1.0'
    }
    shadowJar {
        archiveFileName.set('graal-test.jar')
    }
    jar {
        manifest {
            attributes 'Main-Class': 'io.vproxy.pni.graal.test.Main'
        }
    }

    task compileGraalTestLib(type: Exec) {
        workingDir project.rootProject.rootDir.getAbsolutePath() + '/graal-test/src/main/c'
        def bashPath = System.getenv("MINGW_BASH")
        if (bashPath == null)
            bashPath = '/bin/bash'
        executable bashPath
        args './make-graal-test.sh'

        dependsOn project(':graal-test-template').tasks.generateGraalTesting
    }
    task compileGraalTest(type: Exec) {
        if (OS.isWindows()) {
            executable 'native-image.cmd'
        } else {
            executable 'native-image'
        }
        def output = 'graal-test'
        if (!OS.isWindows()) {
            output += '.run'
        }
        args '-jar', './build/libs/graal-test.jar',
                '--features=io.vproxy.pni.graal.test.Feature2,io.vproxy.pni.graal.test.Feature',
                '--enable-preview', '--enable-native-access=ALL-UNNAMED', '-ea',
                '-Ob', '--no-fallback',
                '-o', output

        dependsOn project(':graal-test-template').tasks.generateGraalTesting
        dependsOn shadowJar

        shadowJar.mustRunAfter(project(':graal-test-template').tasks.generateGraalTesting)
    }
    task runGraalTest(type: Exec) {
        if (OS.isWindows()) {
            executable project.projectDir.getAbsolutePath() + '/graal-test.exe'
        } else {
            executable './graal-test.run'
        }
        args '-Djava.library.path=./src/main/c' +
                File.pathSeparator +
                project.rootProject.rootDir.getAbsolutePath() + '/api/src/main/c'

        dependsOn compileGraalTest
        dependsOn compileGraalTestLib
    }
}

project(":sample") {
    apply plugin: 'com.github.johnrengelman.shadow'

    jar {
        manifest {
            attributes 'Main-Class': 'io.vproxy.pni.sample.Main'
        }
    }
    shadowJar {
        archiveFileName.set('sample.jar')
    }

    compileJava {
        options.compilerArgs += '-parameters'
    }
    dependencies {
        implementation project(":api")
        implementation project(":graal")
        compileOnly 'io.vproxy:graal-feature-mock:1.1.0'
    }

    task generateSample(type: JavaExec) {
        classpath = project(":pni").sourceSets.main.runtimeClasspath
        mainClass = 'io.vproxy.pni.exec.Main'
        systemProperty("io.vproxy.pni.Testing", "true")
        workingDir project.rootProject.rootDir.getAbsolutePath()
        args '-cp', 'sample/build/classes/java/main', '-d', 'sample/src/main/java', '-h', 'sample/src/main/c',
                '-fgraal-native-image-feature=io.vproxy.pni.sample.Feature'

        dependsOn compileJava
    }
    task compilePNISampleLib(type: Exec) {
        workingDir project.rootProject.rootDir.getAbsolutePath() + '/sample/src/main/c'
        def bashPath = System.getenv("MINGW_BASH")
        if (bashPath == null)
            bashPath = '/bin/bash'
        executable bashPath
        args './make-sample.sh'

        dependsOn generateSample
    }
    task runSample(type: JavaExec) {
        classpath = sourceSets.main.runtimeClasspath
        mainClass = 'io.vproxy.pni.sample.Main'
        jvmArgs += '-Djava.library.path=' +
                project.rootProject.rootDir.getAbsolutePath() + '/sample/src/main/c' +
                File.pathSeparator +
                project.rootProject.rootDir.getAbsolutePath() + '/api/src/main/c'

        dependsOn compileJava
        dependsOn compilePNISampleLib
    }
    task sampleNativeImage(type: Exec) {
        if (OS.isWindows()) {
            executable 'native-image.cmd'
        } else {
            executable 'native-image'
        }
        def output = 'sample'
        if (!OS.isWindows()) {
            output += '.run'
        }
        args '-jar', './build/libs/sample.jar', '--features=io.vproxy.pni.sample.Feature', '--enable-preview', '--enable-native-access=ALL-UNNAMED', '--no-fallback', '-o', output

        dependsOn compilePNISampleLib
        dependsOn shadowJar

        shadowJar.mustRunAfter(compilePNISampleLib)
    }
    task runSampleNativeImage(type: Exec) {
        if (OS.isWindows()) {
            executable project.projectDir.getAbsolutePath() + '/sample.exe'
        } else {
            executable './sample.run'
        }
        args '-Djava.library.path=./src/main/c' +
                File.pathSeparator +
                project.rootProject.rootDir.getAbsolutePath() + '/api/src/main/c'

        dependsOn sampleNativeImage
    }
}

project(":playground") {
    sourceSets {
        main {
            java {
                srcDirs = ['src/main/template', 'src/main/java', 'src/main/generated']
            }
        }
    }
    compileJava {
        options.compilerArgs += '-parameters'
    }
    dependencies {
        implementation project(":api")
    }

    task generatePlayground(type: JavaExec) {
        classpath = project(":pni").sourceSets.main.runtimeClasspath
        mainClass = 'io.vproxy.pni.exec.Main'
        workingDir project.rootProject.rootDir.getAbsolutePath()
        args '-cp', 'playground/build/classes/java/main', '-d', 'playground/src/main/generated', '-h', 'playground/src/main/c-generated'

        dependsOn compileJava
    }
    task compilePNIPlaygroundLib(type: Exec) {
        workingDir project.rootProject.rootDir.getAbsolutePath() + '/playground/src/main/c'
        def bashPath = System.getenv("MINGW_BASH")
        if (bashPath != null)
            bashPath = '/bin/bash'
        executable bashPath
        args './make-playground.sh'

        dependsOn generatePlayground
    }
    task runPlayground(type: JavaExec) {
        classpath = sourceSets.main.runtimeClasspath
        mainClass = 'io.vproxy.pni.playground.Main'
        jvmArgs += '-Djava.library.path=' +
                project.rootProject.rootDir.getAbsolutePath() + '/playground/src/main/c' +
                File.pathSeparator +
                project.rootProject.rootDir.getAbsolutePath() + '/api/src/main/c'

        dependsOn compileJava
        dependsOn compilePNIPlaygroundLib
    }
}

project(':performance') {
    apply plugin: 'me.champeau.jmh'
    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java']
                destinationDirectory.set(file('build/classes/java/jmh'))
            }
        }
    }
    jmhRunBytecodeGenerator {
        getJvmArgs().addAll(["--enable-preview", "--enable-native-access=ALL-UNNAMED"])
    }
    dependencies {
        implementation 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
        implementation project(':api')
    }
}

def loadVersion() {
    def PREFIX = "final String _VERSION = \""
    def SUFFIX = "\"; // _THE_VERSION_"
    def ver = file(projectDir.getAbsolutePath() + "/pni/src/main/java/io/vproxy/pni/exec/Main.java")
    def lines = ver.getText().split("\n")
    def isMavenLocalInstall = System.getProperty("MavenLocalInstall", "false").equals("true")
    for (def line : lines) {
        line = line.trim()
        if (line.startsWith(PREFIX) && line.endsWith(SUFFIX)) {
            def verstr = line.substring(PREFIX.length(), line.length() - SUFFIX.length())
            if (verstr.endsWith('-DEV')) {
                if (isMavenLocalInstall) {
                    return "MAVEN-LOCAL"
                } else {
                    verstr = verstr + '-SNAPSHOT'
                }
            }
            return verstr
        }
    }
    return "unknown"
}
